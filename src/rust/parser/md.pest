string_char = _{ (ASCII_ALPHANUMERIC | (!('\u{00}'..'\u{7F}') ~ ANY) | "-" | "â€“" | "_" | "'" | "\"" | "\\*" | "\\$" | "\\[" | "\\]" | " " | "\t" | "," | "." | "!" | "?" | "(" | ")" | "+" | "=" | ";" | ":" | "/" | "%" | "^" | "{" | "}" | "|" | "\\" | ">" | "<" | "~" | "&" | "#" ) }

filepath = { (!"$" ~ !"*" ~ !"\n" ~ !"[" ~ !"]" ~ !">" ~ ANY)+ }

weblink_link = { (!")" ~ ANY)+ }
weblink_text = { string_char+ }

bold_italic_node = { "*"{3} ~ ( named_link_node | latex_block_inline_node | code_block_inline_node | latex_inline_node | code_inline_node | link_node | weblink_node | node)+ ~ "*"{3} }
bold_node = { "*"{2} ~ ( named_link_node | latex_block_inline_node | code_block_inline_node | latex_inline_node | code_inline_node | link_node | weblink_node | node)+ ~ "*"{2} }
italic_node = { "*" ~ ( named_link_node | latex_block_inline_node | code_block_inline_node | latex_inline_node | link_node | weblink_node | node)+ ~ "*" }
named_link_node = { "["{2} ~ filepath ~ "|" ~ node+ ~ "]"{2} }
link_node = { "["{2} ~ filepath ~ "]"{2} }
weblink_node = { "[" ~ weblink_text ~ "]" ~ "(" ~ weblink_link ~ ")"}
square_bracket_node = { "[" ~ (!"]" ~ ANY)+ ~ "]" }
latex_block_inline_node = { "$$" ~ (!"$$" ~ !"\n" ~ ANY)+ ~ "$$" }
code_block_inline_node = { "```" ~ (!"```" ~ !"\n" ~ ANY)+ ~ "```" }
latex_inline_node = { "$" ~ (!"$" ~ ANY)+ ~ "$" }
code_inline_node = { "`" ~ (!"`" ~ ANY)+ ~ "`" }
node = { string_char+ }

heading_line = { "#"{1,6} ~ " " ~ string_line }
numbered_list_line = { (" " | "\r" | "\t")* ~ ASCII_DIGIT+ ~ "." ~ (" " | "\r" | "\t")+ ~ string_line}
list_line = { (" " | "\r" | "\t")* ~ "-" ~ (" " | "\r" | "\t")* ~ string_line}
string_line = { (bold_italic_node | bold_node | italic_node | named_link_node | link_node | weblink_node | square_bracket_node | latex_block_inline_node | code_block_inline_node | latex_inline_node | code_inline_node | node)* }

line = { (heading_line | numbered_list_line | list_line | string_line ) }
block_quote_line = { ((" " | "\r" | "\t")* ~ ">" ~ (block_quote_line | line)) }

code_type = { (ASCII_ALPHANUMERIC | "_" | "-" )+ }
code_block_inner = { (!"```" ~ ANY)* }

block_quote_block = { (block_quote_line ~ NEWLINE)+ }
latex_block = { "$$" ~ (!"$$" ~ ANY)* ~ "$$" }
code_block = { "```" ~ code_type? ~ code_block_inner ~ "```" }
string_block = { ( line ~ NEWLINE )+ }


block = { (block_quote_block | latex_block | code_block | string_block) }

yaml_inner = { (!"---" ~ ANY)* }
yaml = { "---" ~ yaml_inner ~ "---" }

md_file = { SOI ~ yaml? ~ block+ ~ EOI }